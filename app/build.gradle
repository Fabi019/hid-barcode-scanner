plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

final def gitHash = providers.exec {
    commandLine('git', 'rev-parse', '--short=7', 'HEAD')
}.standardOutput.asText.get().trim()

android {
    namespace 'dev.fabik.bluetoothhid'
    compileSdk 34

    defaultConfig {
        applicationId "dev.fabik.bluetoothhid"
        minSdk 28
        targetSdk 34
        versionCode 48
        versionName "1.9.1"

        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitHash}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "default"
    productFlavors {
        bundled {
            dimension = "default"
            manifestPlaceholders = [dep: ""]
        }
        standard {
            dimension = "default"
            manifestPlaceholders = [dep: "barcode"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation "androidx.javascriptengine:javascriptengine:1.0.0-beta01"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation 'androidx.activity:activity-compose:1.9.1'

    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_mat_version"
    implementation 'androidx.compose.runtime:runtime-livedata:1.7.2'
    implementation 'androidx.compose.material3:material3:1.2.1'

    implementation 'androidx.navigation:navigation-compose:2.8.1'
    implementation 'androidx.datastore:datastore-preferences:1.1.1'

    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    bundledImplementation 'com.google.mlkit:barcode-scanning:17.3.0'

    standardImplementation "com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1"
    standardImplementation "com.google.android.gms:play-services-base:18.5.0"

    implementation "com.google.accompanist:accompanist-permissions:$accomp_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}
